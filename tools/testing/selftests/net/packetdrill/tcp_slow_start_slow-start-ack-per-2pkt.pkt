// SPDX-License-Identifier: GPL-2.0
// Test of slow start when not application-limited, so that
// the cwnd continues to grow.
// In this variant, the receiver ACKs every other packet,
// approximating standard delayed ACKs.

// Set up config. To keep things simple, disable the
// mechanism that defers sending in order to send bigger TSO packets.
`./defaults.sh
sysctl -q net.ipv4.tcp_tso_win_divisor=100`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

  +.1 < S 0:0(0) win 32792 <mss 1000,sackOK,nop,nop,nop,wscale 7>
   +0 > S. 0:0(0) ack 1 <mss 1460,nop,nop,sackOK,nop,wscale 8>
  +.1 < . 1:1(0) ack 1 win 257
   +0 accept(3, ..., ...) = 4
   +0 setsockopt(4, SOL_SOCKET, SO_SNDBUF, [200000], 4) = 0

   +0 write(4, ..., 30000) = 30000
   +0 > P. 1:10001(10000) ack 1
   +0 %{ assert tcpi_snd_cwnd == 10, tcpi_snd_cwnd }%

+.105 < . 1:1(0) ack 2001 win 257
   +0 > P. 10001:14001(4000) ack 1

+.005 < . 1:1(0) ack 4001 win 257
   +0 > P. 14001:18001(4000) ack 1

+.005 < . 1:1(0) ack 6001 win 257
   +0 > P. 18001:22001(4000) ack 1

+.005 < . 1:1(0) ack 8001 win 257
   +0 > P. 22001:26001(4000) ack 1

+.005 < . 1:1(0) ack 10001 win 257
   +0 > P. 26001:30001(4000) ack 1

   +0 %{ assert tcpi_snd_cwnd == 20, tcpi_snd_cwnd }%
